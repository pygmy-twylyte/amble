# ========================================
# Location: High Ridge (Start)
# ========================================

room high-ridge {
    name "High Isolated Ridge"
    # player starts here
    visited true
    desc "A small, flat ridge in the midst of a steeply sloped wooded area. Probably west of something, depending on how you're oriented. The only egress seems to be a flight of rough stone steps carved into the slope, which curve upward and then disappear into the trees."
    overlay if item present scrawled_note {
        text "A note scrawled in red marker on bright yellow paper is nailed to a tree trunk near the base of the steps."
    }
    overlay if item present ephemeral_cake {
        text "Strangely, a slice of birthday cake on a paper plate sits precariously balanced on top of a thin boulder nearby."
    }
    exit "up the steps" -> parish-landing
}

# ========================================
# Items
# ========================================

item scrawled_note {
    name "Scrawled Note"
    desc "A note scrawled all caps and red permanent marker on a sheet of yellow legal paper."
    portable true
    location room high-ridge
    text """CANDIDATES: PROCEED TO OFFICE TO OBTAIN VISITOR PASS
           * MUST SHOW VALID INVITATION *
    """
    ability Read
    requires ignite to burn
}

item ephemeral_cake {
    name "Birthday Cake"
    desc "A generous piece of delicious, perfectly iced birthday cake on an embossed paper plate."
    text r#"Embossed around the edge of the plate: "~~~ From GLaDOS and Aperture Science Laboratories ~~~" "#
    location room high-ridge
    portable true
    ability Eat
    ability Read
    consumable {
        uses_left 1
        consume_on ability Eat
        when_consumed despawn
    }
}

item no_cake {
    name "No Cake"
    desc "You carry an uneasy feeling where a dessert might be. It is the absence of cake, and you just can't seem to get past it."
    portable true
    location nowhere "spawns when player tries to eat ephemeral_cake"
}

# ========================================
# Triggers
# ========================================

trigger "[Global] Cannot Drop No Cake"
when drop item no_cake
{
    do despawn item no_cake
    do spawn item no_cake in inventory
    do show "Try as you might, you cannot rid yourself of the feeling of a missing, promised dessert. The cake was a lie."
}

trigger "Burn After Reading?"
when act burn on item scrawled_note {
    do show "You burn the note, singeing your fingers in the process. Do you think this is a spy novel?"
    do despawn item scrawled_note
}

trigger "[High-Ridge] Read Scrawled Note" only once when use item scrawled_note ability read {
    do add flag read-scrawled-note
}

trigger "The Cake Is A Lie"
when eat item ephemeral_cake {
    do show "Just as you try to take a bite of the cake, it vanishes without a trace."
    do spawn item no_cake in inventory
    # explicit despawn of ephemeral cake not needed (single use consumable)
    do award points 2 reason "Confirmed the cake was a lie"
}

# ========================================
# Goals
# ========================================

goal get-oriented {
    name "What the...?"
    desc "Look around and read the note to get some idea what you're doing here. Type 'help' for some basic guidance."
    group optional
    done when has flag read-scrawled-note
}
