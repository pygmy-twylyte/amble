# ========================================
# Location: High Ridge (Start)
# ========================================

room high-ridge {
    name "High Isolated Ridge"
    # player starts here
    visited true
    desc "A small, flat ridge in the midst of a steeply sloped wooded area. Probably west of [[i]]something[[/i]], depending on how you're oriented. The only egress seems to be a flight of rough stone steps carved into the slope, which curve upward and then disappear into the trees."
    overlay if item present scrawled_note {
        text "A [[item]]note[[/item]] scrawled in green marker on bright yellow paper is nailed to a tree trunk near the base of the steps."
    }
    overlay if item present ephemeral_cake {
        text "Strangely, a slice of [[item]]birthday cake[[/item]] on a paper plate sits precariously balanced on top of a thin boulder nearby."
    }
    exit "up the steps" -> parish-landing
    scenery "Stone Steps" desc "Worn smooth by time and weather, you can still see where these giant slabs were shaped by the hand tools of ancient craftsmen."
    scenery "Paper Plate"
    scenery "Boulder"
}

item scenery_trees {
    name "Trees"
    desc "A faded sign stapled to one tree reads: [[box]]№ 1 — [[i]]The Larch[[/i]][[/box]]"
    location room high-ridge
    movability fixed "Seriously? They're rooted into the ground and twenty times your size!"
    visibility scenery
    aliases "woods", "forest"
}

# ========================================
# Items
# ========================================

item scrawled_note {
    name "Scrawled Note"
    desc "A note scrawled in [[b]]ALL CAPS[[/b]] and green permanent marker on a sheet of yellow legal paper."
    movability free
    location room high-ridge
    text """[[box]]CANDIDATES: PROCEED TO OFFICE TO OBTAIN VISITOR PASS
           * MUST SHOW VALID INVITATION *[[/box]]"""
    ability Read
    requires ignite to burn
    aliases "paper"
}

item ephemeral_cake {
    name "Birthday Cake"
    desc "A [[b]][[i]]generous[[/i]][[/b]] piece of delicious, perfectly iced birthday cake on a paper plate [[i]]embossed with a message.[[/i]]"
    text r#"[[center]]"~~~ From GLaDOS and Aperture Science Laboratories ~~~"[[/center]][[center]][[i]](embossed on the edge of the plate)[[/i]][[/center]]"#
    location room high-ridge
    movability free
    ability Eat
    ability Read
    consumable {
        uses_left 1
        consume_on ability Eat
        when_consumed despawn
    }
}

item no_cake {
    name "No Cake"
    desc "You carry an uneasy feeling where a dessert might be. It is the [[highlight]]absence[[/highlight]] of cake, and you just can't seem to get past it."
    movability free
    location nowhere "spawns when player tries to eat ephemeral_cake"
}

# ========================================
# Triggers
# ========================================

trigger "[Global] Cannot Drop No Cake"
when drop item no_cake
{
    do despawn item no_cake
    do spawn item no_cake in inventory
    do show "Try as you might, you cannot rid yourself of the feeling of a missing, promised dessert. The cake was a lie."
}

trigger "Burn After Reading?"
when act burn on item scrawled_note {
    do show "You burn the note, singeing your fingers in the process. Do you think this is a spy novel?"
    do despawn item scrawled_note
}

trigger "[High-Ridge] Read Scrawled Note" only once
when use item scrawled_note ability read {
    do add flag read-scrawled-note
}

trigger "The Cake Is A Lie"
when eat item ephemeral_cake {
    do show "Just as you try to take a bite of the cake, it vanishes without a trace."
    do spawn item no_cake in inventory
    # explicit despawn of ephemeral cake not needed (single use consumable)
    do award points 2 reason "Confirmed the cake was a lie"
}

# ========================================
# Goals
# ========================================

goal get-oriented {
    name "What the...?"
    desc "Look around and read the [[item]]scrawled note[[/item]] at [[room]]High Isolated Ridge[[/room]] to get some idea what you're doing here. Type 'help' for some basic guidance."
    group optional
    done when has flag read-scrawled-note
}
