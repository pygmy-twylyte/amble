# ========================================
# Woodland: Two Sheds Landing
# ========================================
# Related: puzzles/clear_fallen_tree

room two-sheds-landing {
    name "Jackson's Landing"
    desc "A quiet landing tucked along the slope, with a rusted mailbox labeled 'A. Jackson' standing to one side. A trail sign points east toward something optimistically named [[b]]\"Two Sheds\"[[/b]]. Underneath, someone has scratched in: [[i]]\"Technically One.\"[[/i]] Rough steps lead down the slope, and continue curving upward."

    overlay if ( item present fallen_tree ) {
        text "A [[item]]dense tangle of limbs and branches[[/item]] from a fallen tree block the path continuing upward."
    }
    overlay if ( flag set chopped-fallen-tree, flag unset saw-paul-bunyan ) {
        # these would be gone if player sees paul bunyan at guard-post
        text "Cut branches of a fallen tree are piled to the side of the path upward."
    }
    overlay if ( flag set burned-fallen-tree ) {
        text "Charred twigs and smoldering ash are all that remain of the fallen tree."
    }
    exit "up the slope" -> guard-post {
        required_flags( cleared-fallen-tree ),
        barred "You can't get around the fallen tree. You'll need to find a way to clear it from the path."
    }
    exit "down the steps" -> parish-landing
    exit "trail east" -> two-sheds
}

# ========================================
# Items
# ========================================

item jackson_mailbox {
    name "Mailbox"
    desc "A typical white mailbox with a red flag on a post, situated at the top of a path into the woods to the east."
    movability fixed "It's screwed to the post, which is buried in a concrete base."
    location room two-sheds-landing
    container state closed
}

item aa_shed_notice {
    name "Official Notice"
    desc "An official-looking notice from Amble Adventures HQ."
    movability free
    location chest jackson_mailbox
    text """[[center]][[bright-red]][[b]][[u]] OFFICIAL NOTICE [[/u]][[/b]][[/bright-red]][[/center]][[center]]The OverShed™ Division of Amble Adventures has reclassified this dwelling as:[[/center]]

[[box]]Shed, Subtype B – [[hl]]Multiversal Crosslink Node[[/hl]][[/box]]

[[center]]Report any fictional apparitions or missing time to your local Amble Adventures cover-up coordinator.[[/center]]
"""
    ability Read
}
