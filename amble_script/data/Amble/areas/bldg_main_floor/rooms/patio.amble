# ========================================
# Restaurant Patio
# ========================================

# Starts with description merely as blinding explosion of light, so player doesn't get to peek at the contents early
room patio {
    name "BLINDING GLARE"
    desc "You see almost nothing at all, except for a momentary glimpse of a few scattered details before you are blinded. The pure white light here is stare-into-the-sun bright and seems to come from all directions, forcing you to shield your eyes with your arm and flee."
    overlay if player has item peril_sunglasses, flag set patio-gate-open {
        text "The Stargate shimmers off to the side by the kitchen door, its connection to the laboratory still open."
    }
    overlay if player has item peril_sunglasses, flag unset patio-gate-open {
        text "A large metallic ring stands by the kitchen door. It might be a backdrop for weddings, but it's covered in hieroglyphs and other odd symbols -- none of them rings, hearts, doves, or branding irons."
    }
    exit west -> restaurant
}
# Triggers alter room appearance when the sunglasses are taken or dropped
trigger "Modify Patio: Sunglasses On" when take item peril_sunglasses {
    do modify room patio {
        name "Restaurant Patio"
        desc "The patio overlooks nothing in particularâ€”just an endless starfield gently swirling beyond a transparent force field. A few metal tables are bolted to the floor, and one of them has a half-empty cocktail glass slowly un-drinking itself. A sign near the edge reads: 'No Leaning Beyond This Point. You have been warned.'"
    }
}
trigger "Modify Patio: Sunglasses Off" when drop item peril_sunglasses {
    do modify room patio {
        name "BLINDING GLARE"
        desc "You see nothing at all. The pure white light here is direct-sunlight bright and seems to come from all directions, forcing you to shield your eyes with your arm and flee."
    }
}

# ========================================
# Items
# ========================================
#
item un_drink {
    name "Negative Cocktail"
    desc "A crystal glass sits half full of... nothing. And yet, the liquid level keeps rising, as if the drink is slowly returning to the bartender across time. The stir stick spins gently in defiance of gravity and good sense."
    portable true
    location room patio
    ability Drink
    consumable {
        uses_left 1
        consume_on ability Drink
        when_consumed despawn
    }
}

item plaque_3 {
    name "Crystal Plaque"
    desc "An intricate pure clear crystal plaque with an inlaid inscription in obsidian; it must have taken decades to create -- atop an ornately carved pillar that probably took nearly as long to create -- impressing the beholder with the sheer enormity of its message."
    portable false
    location room patio
    ability Read
    text "Need a pointless but entertaining quote here."
}

# ========================================
# Triggers
# ========================================

trigger "[Plaque 3] Read the Plaque" only once
when use item plaque_3 ability read {
    do add flag read-plaque-3
    do award points 10
}

trigger "[Patio] Sunglasses Required" when enter room patio {
    if missing item peril_sunglasses {
        do push player to restaurant
        do show "A blinding burst of light erupts from the swirling starfield, forcing you to shield your eyes. With no protection, you're driven back into the relative safety of the restaurant."
    }
    if has item peril_sunglasses {
        do show "Sensing the possible harm in eating a dessert a stone's throw away from the explosions of all nearby stars, your peril-sensitive sunglasses flash nearly completely opaque, allowing you to see without permanent damage to the retina as the galaxies outside continue to erupt."
    }
}

trigger "[Patio] Award Points Getting In" only once
note "separate one-off trigger to award first time getting in"
when enter room patio {
    if has item peril_sunglasses {
        do award points 2
    }
}
