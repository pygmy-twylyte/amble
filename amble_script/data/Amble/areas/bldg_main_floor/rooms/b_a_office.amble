# ==========================================
# Brains Anonymous / Amble Adventures Office
# ==========================================

room b-a-office {
    name "Amble Adventures Main Office"
    desc "The office of Amble Adventures, a wholly disowned subsidiary of Brains Anonymous™, is a shrine to barely-funded creativity. Faded posters of games that were never released adorn the walls, including one titled 'PONG Construction Set 3000'. A sagging ficus looms in the corner like it’s been through something. An inconspicuous door in the north wall is marked with a \"Supplies\" sign, hanging at a carefree slant. An Initech™ Poetry Performer plays possum in another corner, hoping you don't notice it."
    overlay if flag panic-room-open {
        set r#"The "Planning to Plan" whiteboard has parted to reveal a heavy, vault-like door set into the west wall."#
        unset r#"A floor-to-ceiling whiteboard on the west wall boasts an impressive flowchart titled "Planning to Plan"."#
    }
    overlay if (flag unset got-visitor-pass, player missing item invitation) {
        text "A robotic receptionist sits behind a tidy desk adorned with figurines of Portal turrets and companion cubes. She (it?) pointedly ignores you."
    }
    overlay if (flag unset got-visitor-pass, player has item invitation) {
        text "The receptionist, sensing something, tries furtively to see what you have in your pack."
    }
    overlay if (flag set got-visitor-pass) {
        text "The receptionist looks up from rearranging her turrets, giving you a subtle wink and a grin."
    }
    overlay if (flag set lost-and-found-opened) {
        text "The Lost & Found box sits with its lid hanging open, contents plainly visible."
    }
    exit "front door" -> main-lobby
    exit "closet door" -> supply-closet
}

# ========================================
# Items
# ========================================
# item poetry_performer is in keycard puzzle

item peril_sunglasses {
    name "Stylish Sunglasses"
    desc "A pair of stylish black shades that automatically go completely opaque at the first sign of danger. Unfortunately, this includes things like loud noises, sudden movement, or mild existential dread."
    portable true
    location chest lost_and_found_box
}

item lost_and_found_box {
    name "Lost & Found Box"
    desc "A sturdy metal lock box with an official-looking sticker reading: 'Lost & Found — Amble Adventures Division' and 'Caution: Contents May Be Fictional'."
    portable false
    location room b-a-office
    container state locked
}

item zircon_tweezers {
    name "Zircon-Encrusted Tweezers"
    desc "A heavy-duty pair of tweezers, encrusted in zircon for extra dazzle and especially good for plucking."
    portable true
    location chest lost_and_found_box
    ability Pluck
}

item melted_aa_3b_id {
    name "Section AA-3 Staff ID"
    desc "A facility ID card for sector AA-3. Its weight seems to change as you hold it, as does the temperature. It's not warped or melted exactly -- just *distorted* somehow."
    portable true
    location chest lost_and_found_box
    text """ RESTRICTED ACCESS PASS
SECTOR AA-3, HIGH ENERGY
 REALITY BRIDGE PROJECT
"""
    ability Read
}

# ========================================
# Triggers
# ========================================

trigger "[Office] Open Poetry Performer"
when open item poetry_performer {
    do show "The machine clicks on and hums. A flashing red light warns:\n            ⚠️ NOT FOR USE WITH VOGON POETRY ⚠️"
}

trigger "[Office] Gifted Lost and Found Key" only once
when enter room b-a-office {
    if all(has flag got-elevator-keycard, with npc b_a_receptionist) {
        do give item lost_and_found_key to player from npc b_a_receptionist
        do show "Seeing you return with the keycard, the receptionist looks around warily and slips something into your hand, then looks directly at the lost and found box."
        do npc says b_a_receptionist "Finally! A Candidate bright enough to get to that keycard. I guess I lost the pool. Who'd have thought it'd be a primate?"
        do npc says b_a_receptionist "(*whispers*) Use this before you head down to AA-3B."
    }
}

trigger "[Office] Lost and Found Unlocked" only once when unlock item lost_and_found_box {
    do award points 3
    do add flag lost-and-found-opened
    do set item description lost_and_found_box "A sturdy metal lockbox marked 'Lost and Found'. Its lid is cracked slightly open."
}

# ========================================
# Goals
# ========================================

goal find-the-office {
    name "Find the Office"
    desc "According to the posted note, there's an office somewhere around here and you need to check in. You'll need to find it first."
    group required
    start when goal complete get-oriented
    done when reached room b-a-office
}

goal get-visitor-pass {
    name "Get a Visitor Pass"
    desc "Now that you have an invitation, maybe the receptionist in the office will help you with this -- or maybe she won't."
    group required
    start when goal complete get-invited
    done when has flag got-visitor-pass
}
