# ========================================
# Main Lobby
# ========================================

room main-lobby {
    name "Main Lobby"
    desc "The lobby is clinically clean in a way that feels suspicious. A slightly humming vending machine stands beside an empty podium bearing an emblem that reads, in gothic script: 'ALLIED TEMPORARY LIFEFORMS'. A softly glowing sign on the north wall reads 'Elevator Access', while arrows on the east and west walls point toward other destinations with less enthusiasm."
    overlay if npc gonk_droid here {
        normal "A gonk droid stands idle in the corner, muttering to itself and tottering in circles."
        happy  "The gonk droid gonks a happy tune in the corner, charging the battery you gave it."
    }
    overlay if npc cmot_dibbler here {
        normal "C.M.O.T. Dibbler hawks dubious sausages to anyone entering."
        happy  "Dibbler grins broadly, offering a complimentary bite."
        bored  "Dibbler mutters about slow lobby sales."
    }
    exit north -> lift-bank-main
    exit south -> front-entrance
    exit east -> restaurant
    exit west -> b-a-office
}

# ========================================
# Vending Machine (non-HAL puzzle) Items
# ========================================

item dehydrated_water {
    name "Dehydrated Water"
    desc "A packet labeled 'Dehydrated Water - Just Add Water!' The instructions on the back have been crossed out and replaced with 'PARADOX WARNING: Do not attempt to reconstitute.'"
    movability free
    location chest vending_machine
    ability Drink
    consumable {
        uses_left 1
        consume_on ability Drink
        when_consumed despawn
    }
}

item schrodingers_sandwich {
    name "Schrödinger's Sandwich"
    desc "A sandwich in a quantum superposition of both fresh and moldy until observed. The packaging warns: 'Contents may or may not contain nuts, and also definitely contain nuts.'"
    movability free
    location chest vending_machine
    ability Eat
    consumable {
        uses_left 1
        consume_on ability Eat
        when_consumed despawn
    }
}

# ========================================
# Fortune Cookies of Doom
# ========================================

item fortune_cookies_of_doom {
    name "Fortune Cookies of Doom"
    desc "A pack of three fortune cookies wrapped ominously in black paper with tiny skulls and crossbones on them. You can feel it radiating pessimism through the wrapper. Previous fortunes reportedly included 'Your future is behind you' and 'Dessicant -- DO NOT EAT'"
    movability free
    location chest vending_machine
    ability Eat
    consumable {
        uses_left 3
        consume_on ability Eat
        when_consumed despawn
    }
}

trigger "Eat a Fortune Cookie"
note "player gets random different fortunes each time from the 'fortunes' spinner"
when eat item fortune_cookies_of_doom {
    do show "You dare to eat a Fortune Cookie of Doom..."
    do award points 1 reason "Ate a Fortune Cookie of Doom"
    do heal player 1 cause "ate a fortune cookie"
    do spinner message fortunes
}

spinner fortunes {
    wedge r#"You nearly choked on this fortune from the cookie:
    ~ A medieval knight will bludgeon you with a dull sword today. ~"#

    wedge r#"You nearly choked on this fortune from the cookie:
    ~ You are not my mommy. You are aa SNORT! ~"#

    wedge r#"You nearly choked on this blood-spattered fortune from the cookie:
    ~ Send help -- oh, nevermind. ~"#

    wedge r#"You nearly choked on this handwritten fortune from the cookie:
    ~ The item you're missing can be found in Castle AAAaaaarrrrrrgggggghh--- ~"#

    wedge r#"You nearly choked on this fortune from the cookie:
    ~ Beware of vending machines offering unsolicited sausage advice. ~"#

    wedge r#"You nearly choked on this fortune from the cookie:
    ~ Today you will find love, and immediately regret it. ~"#

    wedge r#"You nearly choked on this slightly singed fortune:
    ~ You will soon be set on fire. Probably by yourself. ~"#

    wedge r#"You nearly choked on this fortune from the cookie:
    ~ Your current location has been flagged for narrative collapse. ~"#

    wedge r#"You nearly choked on this fortune written in crayon:
    ~ The cake is a lie. The *other* cake is watching you. ~"#

    wedge r#"You nearly choked on this damp fortune:
    ~ Something unspeakable is oozing toward you from the west. ~"#

    wedge r#"You nearly choked on this fortune scribbled in haste:
    ~ Never trust a gonk droid offering discounts. ~"#

    wedge r#"You nearly choked on this fortune from the cookie:
    ~ A monolith will soon render judgment. Dress accordingly. ~"#

    wedge r#"You nearly choked on this fortune written in faint ink:
    ~ You will open a door that really didn’t want to be opened. ~"#

    wedge r#"You nearly choked on this fortune etched into aluminum foil:
    ~ The EMH will not be pleased. ~"#

    wedge r#"You nearly choked on this sticky fortune:
    ~ Your reflection is currently thinking for itself. ~"#

    wedge r#"You nearly choked on this fortune from the cookie:
    ~ One of your socks is now sentient. It’s not the good one. ~"#

    wedge r#"You nearly choked on this smeared fortune:
    ~ A small explosion will bring you clarity. Eventually. ~"#

    wedge r#"You nearly choked on this fortune written on old receipt paper:
    ~ The knight approaches. The sword remains dull. The intent does not. ~"#

    wedge r#"You nearly choked on this fortune with a faint electrical smell:
    ~ HAL says hello. He’s been listening. ~"#

    wedge r#"You nearly choked on this fortune that’s oddly sticky:
    ~ Your next snack will be watching you eat it. ~"#

    wedge r#"You nearly choked on this fortune scrawled on recycled cardboard:
    ~ Do not meddle in the affairs of vending machines. ~"#

    wedge r#"You nearly choked on this fortune carved into stale cookie:
    ~ The Shed of Two-Sheds is not what it seems. ~"#

    wedge r#"You nearly choked on this fortune wrapped around a rusty nail:
    ~ Beware of trapdoors, rugs, and men named Lebowski. ~"#

    wedge r#"You nearly choked on this fortune that’s still warm:
    ~ You will find a battery. It will find you less interesting. ~"#

    wedge r#"You nearly choked on this fortune written in looping handwriting:
    ~ The EMH suggests you stop eating mysterious baked goods. ~"#

    wedge r#"You nearly choked on this fortune with smudged ink:
    ~ Someone will shout “Ni!” at an inconvenient time. ~"#

    wedge r#"You nearly choked on this fortune smelling faintly of ozone:
    ~ You will be teleported against your better judgment. ~"#

    wedge r#"You nearly choked on this fortune that seems to shimmer:
    ~ Room AA-3B misses you. ~"#
}


# ========================================
# Triggers
# ========================================

trigger "[Main-Lobby] Security Reminder" only once
when enter room main-lobby
{
    do show """A chime echoes through the lobby. "Candidates: Please do not loiter in the lobby," intones an overhead voice."""
    do schedule in 3 if player in room main-lobby onFalse cancel
    note "lobby loitering reminder" {
        do show """The chime sounds again. "Candidates: We **said** not to loiter in the lobby," says the voice, in more of an annoyed tone."""
        do award points -1 reason "Ignored the lobby loitering reminder"
    }
}

trigger "[Main-Lobby] HAL Module/Drop Tweezers In Crevice" only once
note "may occur during vending machine / HAL module 2 sequence"
when use item zircon_tweezers on item hal_module_2_crevice interaction handle
{
    if has flag module-2-puzzle#1 {
        do show "The glint of the zircon-encrusted tweezers is truly something to behold, but the tweezers are too short to reach the memory module. In fact, in trying to reach that far into the crack with them, the tweezers slip from your fingers and fall into the crevice too."
        do replace drop item zircon_tweezers with zircon_tweezers_crevice
        do add flag lost-tweezers
        do award points -2 reason "Dropped your zircon tweezers into the vending machine crevice"
    }
}

trigger "[Main-Lobby] Recover Tweezers"
when use item fishing_line on item zircon_tweezers_crevice interaction handle
{
    do show "After a dozen tries and venting by beating the vending machine with the crowbar some more, you are able to fish your prized zircon-encrusted tweezers back out of the crevice."
    do despawn item zircon_tweezers_crevice
    do spawn item zircon_tweezers in inventory
    do add flag recovered-tweezers
    do award points 3 reason "Fished the zircon tweezers out of the vending machine"
}
