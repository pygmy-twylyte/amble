room lift-bank-main {
    name "Elevator Bank (Main Floor)"
    desc "A bank of elevators waits here, none of which seem eager to go anywhere. One of them is open and labeled 'MAIN LIFT' in glowing teal script.  To the east, a door marked 'LOUNGE' is just slightly ajar, as if luring you to come in. The lobby is still visible through the archway south."
    overlay if flag got-elevator-keycard {
        set "The proximity card reader next to the lift now somewhat sheepishly displays: ACCESS GRANTED."
        unset "The proximity card reader next to the lift displays: ACCESS DENIED -- and with no sympathy whatsoever."
    }
    overlay if npc gonk_droid here {
        normal "A gonk droid pokes at the elevator call panel, emitting perplexed gonks."
        happy  "The gonk droid hums a cheerful gonk as it watches the lift lights blink."
    }
    overlay if npc cmot_dibbler here {
        normal "Dibbler loiters by the elevators, promising snacks for the ride."
        happy  "Dibbler beams, certain elevator patrons are his best customers."
        bored  "Dibbler counts his sausages as the lift lights blink listlessly."
    }
    exit "into the elevator" -> lift-main
    exit "to the lounge" -> lounge
    exit "through the archway" -> main-lobby
}

# ========================================
# Triggers
# ========================================

trigger "[Lift-Bank-Main] Keycard Activates Elevator" when enter room lift-bank-main {
    if has item elevator_keycard {
        do show "The [[item]]keycard[[/item]] in your pack chirps twice as you approach the proximity reader on the wall, which responds with a happy melody."
    }
}

trigger "[Lift-Bank-Main] Point Award On First Activation" only once
when enter room lift-bank-main {
    if has item elevator_keycard {
        do add flag elevator-activated
        do show "The main lift lights up, and Muzak plays over a tinny speaker inside. In horror, you realize: It's Porcupine Tree's [[b]]'The Sound of Muzak'[[/b]] -- [[i]]only the Muzak version.[[/i]]"
        do award points 3 reason "Activated the main lift with the keycard"
    }
}

# ========================================
# Goals
# ========================================

goal activate-elevator {
    name "Activate the Elevator"
    desc "Now that you have both a visitor pass and the proximity keycard, you should be able to use the elevator off the main lobby."
    group required
    start when goal complete get-elevator-keycard
    done when has flag elevator-activated
}

goal get-elevator-keycard {
    name "Get the Elevator Keycard"
    desc "The receptionist said the last Candidate had the proximity card just before it melted in the panic room. (The Candidate melted, not the card.) It may still be there."
    group required
    start when goal complete get-visitor-pass
    done when has flag got-elevator-keycard
}
