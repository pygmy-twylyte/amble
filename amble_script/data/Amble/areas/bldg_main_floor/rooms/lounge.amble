room lounge {
    name "Coffee Lounge"
    desc "A small space to relax, with a worn sofa, a ringed coffee table bearing a few magazines, and part of a jigsaw puzzle depicting the USS Enterprise D."
    overlay if item lebowski_rug {
        present "An ornate, brick red rug lies in the center of the room and somehow -- really ties the room together."
        absent "The trap door opened in the floor reveals a ladder down to a hidden room below."
    }
    overlay if ( flag set lab-fire-raging ) {
        text "Occasional wisps of black smoke rise from the portal room below."
    }
    overlay if flag coffee-ready {
        set "The coffee machine emits a few satisfied clicks as the rich aroma of fresh coffee fills the lounge."
        unset "An automatic drip coffee machine stands ready to brew on a small stand in one corner."
    }
    overlay if npc gonk_droid here {
        normal "The gonk droid mills about by the coffee cart, muttering quiet gonks."
        happy  "The gonk droid gonks to an unheard upbeat rhythm by the sofa."
    }
    overlay if npc cmot_dibbler here {
        normal "Dibbler lounges here, pitching snacks to passing staff and adventurers."
        happy  "Dibbler cheerfully offers samples to anyone near the sofa."
        bored  "Dibbler stares at the coffee pot, wondering if he can sell the used grounds somehow."
    }
    exit "lounge door" -> lift-bank-main
}

# ========================================
# Items
# ========================================

item lebowski_rug {
    name "Ornate Rug"
    desc "It really ties the room together, man."
    movability free
    location room lounge
    requires ignite to burn
}

item coffee_machine {
    name "Coffee Machine"
    desc "A sleek black single-serve drip coffee machine, ready to go with fresh grounds."
    movability fixed "It's tied directly into the water supply pipe."
    location room lounge
    ability TurnOn
}

item hot_coffee {
    name "Hot Coffee"
    desc "A paper cup of piping hot deep black coffee. Smells like a dark french roast."
    movability free
    location nowhere "spawns@lounge after coffee_machine started"
    ability Drink
    consumable {
        uses_left 2
        consume_on ability Drink
        when_consumed despawn
    }
}

item cooled_coffee {
    name "Cooled Coffee"
    desc "A paper cup of formerly-hot coffee, cooled down to room temperature now."
    movability free
    location nowhere "spawns when hot coffee cools down"
    ability Drink
    consumable {
        uses_left 1
        consume_on ability Drink
        when_consumed despawn
    }
}

# ========================================
# Triggers
# ========================================

trigger "Coffee Cools Down" only once
when take item hot_coffee {
    do schedule in 7 note "coffee cooled" {
        do replace item hot_coffee with cooled_coffee
    }
}

trigger "Hot Coffee Healing"
when drink item hot_coffee {
    do heal player 5 cause "hot coffee healing"
}

trigger "Cool Coffee Heals Too (less)"
when drink item cooled_coffee {
    do heal player 2 cause "room-temperature coffee"
}

trigger "[Lounge] Rug Taken / Reveal Trapdoor" only once
when take item lebowski_rug {
    do reveal exit from lounge to portal-room direction down
    do add flag found-portal-room
    do award points 3 reason "Found the trapdoor beneath the rug"
    do show "Light shines around the edges of a trap door revealed in the floor when you remove the rug. The room no longer seems tied together."
}

trigger "[Lounge] Rug Burned Away" only once
when act burn on item lebowski_rug {
    do despawn item lebowski_rug
    do reveal exit from lounge to portal-room direction down
    do add flag found-portal-room
    do award points 3 reason "Revealed the trapdoor by torching the rug"
    do show "When the acrid smoke clears and your eyes stop burning, you can make out a trap door revealed in the floor where the rug used to be."
    do add wedge "Security Announcement: Some arsonist burned the rug in the lounge. It no longer ties the room together. This aggression will not stand, man." width 1 spinner ambientInterior
}

trigger "[Lounge] Brew Coffee" only once
when use item coffee_machine ability turnOn
{
    do show "The coffee machine hums to life and begins its brewing cycle. A timer shows 3 minutes remaining..."
    do set item description coffee_machine "A single serve drip coffee machine, which apparently takes quite a while to finish brewing a cup one drip at a time..."
    do schedule in 3 note "coffee done brewing" {
        do set item description coffee_machine "A single serve, drip coffee maker. The brew cycle is complete and the grounds have already been used."
        do show "♪ BEEP BEEP BEEP! ♪ The coffee machine chimes cheerfully. The rich aroma of freshly brewed coffee fills the air."
        do spawn item hot_coffee into room lounge
        do add flag coffee-ready
        do award points 2 reason "Brewed a cup of hot coffee"
    }
}

trigger "[Lounge] Coffee Taken"
when take item hot_coffee
{
    do remove flag coffee-ready
}
