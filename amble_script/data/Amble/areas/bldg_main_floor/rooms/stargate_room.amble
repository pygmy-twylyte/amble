# ========================================
# Stargate Room
# ========================================
# This serves as a fast-travel / teleportation hub and should only be discovered
# after a fair bit of exploring. The plaque mini-puzzles will serve as receiver
# locations.

room stargate-room {
    name "Stargate Room"
    desc """A hangar-size concrete room, connected to the Aperture lab through a huge, round vault door. Little light reaches the darkened corners of the room, but at its center stands a round control console ringed with hieroglyphs, obscure emojis, and diagrams from IKEA assembly manuals. A giant metallic ring festooned with similar symbols stands on end at the end of the room.\n\nThere are three large buttons in the center of the console:"""

    overlay if flag read-plaque-1 {
        set   "On the left, shaped like a tree:        [[green]]RAISED[[/green]]"
        unset "On the left, shaped like a tree:        [[red]][[dimmed]]sunken[[/dimmed]][[/red]]"
    }
    overlay if flag read-plaque-2 {
        set   "In the middle, shaped like a snowflake: [[green]]RAISED[[/green]]"
        unset "In the middle, shaped like a snowflake: [[red]][[dimmed]]sunken[[/dimmed]][[/red]]"
    }
    overlay if flag read-plaque-3 {
        set   "On the right, shaped like a fork:       [[green]]RAISED[[/green]]"
        unset "On the right, shaped like a fork:       [[red]][[dimmed]]sunken[[/dimmed]][[/red]]"
    }
    exit "vault door" -> aperture-lab
}

# ========================================
# Activate Woodland Gate
# ========================================

item woodland_button {
    name "[ Tree ]"
    desc "A tree shaped button on the left of the Stargate control console."
    movability fixed "It's part of the control console."
    location room stargate-room
}

trigger "[Stargate] Press Woodland Button"
note "open/move the Stargate connection to Parish Landing (only if plaque-1 read)"
when touch item woodland_button {
    if has flag read-plaque-1 {
        do show "With a great whooshing like the flush of a titan's toilet, the Stargate opens a new connection. You hear singing birds and the occasional screech coming through the opening."
        # modify stargate room
        do modify room stargate-room {
            desc "A hangar-size concrete room, connected to the Aperture lab through a huge, round vault door. The Stargate is filled with a shimmering energy, from which you hear whispering sound and birdsong. Occasionally, a bird flies out of the gate and circles it, but -- finding nowhere to land -- flies back in."
            # add exit to parish-landing and remove either of the others if previously opened
            add exit "through the stargate" -> parish-landing
            remove exit ice-pit
            remove exit patio
        }
        # modify receiving rooms
        # open woodland gate
        do add flag woodland-gate-open
        do modify room parish-landing {
            add exit "through the stargate" -> stargate-room
        }
        # close snowfield gate
        do remove flag snowfield-gate-open
        do modify room ice-pit {
            remove exit stargate-room
        }
        # close patio gate
        do remove flag patio-gate-open
        do modify room patio {
            remove exit stargate-room
        }
    }
    if missing flag read-plaque-1 {
        do show "The tree button is already pressed down as far as it can go; you can't activate it."
    }
}

# ========================================
# Activate Snowfield Gate
# ========================================

item snowfield_button {
    name "[ Snowflake ]"
    desc "A snowflake-shaped button ing the middle of the Stargate control console."
    movability fixed "It's part of the control console."
    location room stargate-room
}

trigger "[Stargate] Press Snowfield Button"
note "open/move the Stargate connection to Ice Pit (only if plaque 2 read)"
when touch item snowfield_button {
    if has flag read-plaque-2 {
        do show "With a great whooshing like the flush of a titan's toilet, the Stargate opens a new connection. You are chilled by the frigid air pouring through the opening."
        # modify stargate room
        do modify room stargate-room {
            desc "A chilly, hangar-size concrete room, connected to the Aperture lab through a huge, round vault door. The Stargate is filled with a shimmering energy, from which you hear howling wind. Occasionally, another unearthly howl (Zomby Woof?) joins in."
            # add exit to parish-landing and remove either of the others if previously opened
            add exit "through the stargate" -> ice-pit
            remove exit parish-landing
            remove exit patio
        }

        # modify receiving rooms

        # close woodland gate
        do remove flag woodland-gate-open
        do modify room parish-landing {
            remove exit stargate-room
        }
        # open snowfield gate
        do add flag snowfield-gate-open
        do modify room ice-pit {
            add exit "through the stargate" -> stargate-room
        }
        # close patio gate
        do remove flag patio-gate-open
        do modify room patio {
            remove exit stargate-room
        }
    }
    if missing flag read-plaque-2 {
        do show "The snowflake button is sunken; you can't push it down any farther."
    }
}

# ========================================
# Activate Patio Gate
# ========================================

item patio_button {
    name "[ Fork ]"
    desc "A fork-shaped button on the right of the Stargate control console."
    movability fixed "It's part of the control console."
    location room stargate-room
}

trigger "[Stargate] Press Patio Button"
note "open/move the Stargate connection to the Patio (only if plaque-3 read)"
when touch item patio_button {
    if has flag read-plaque-3 {
        do show "With a great whooshing like the flush of a titan's toilet, the Stargate opens a new connection. You are chilled by the frigid air pouring through the opening."
        # modify stargate room
        do modify room stargate-room {
            desc "A hangar-size concrete room, connected to the Aperture lab through a huge, round vault door. The Stargate is filled with a shimmering energy, from which you hear the sounds of clinking glasses punctuated by explosions. Occasionally, a bird flies out of the gate and circles it, but -- finding nowhere to land -- flies back in."
            # add exit to parish-landing and remove either of the others if previously opened
            add exit "through the stargate" -> patio
            remove exit ice-pit
            remove exit parish-landing
        }
        # modify receiving rooms

        # close woodland gate
        do remove flag woodland-gate-open
        do modify room parish-landing {
            remove exit stargate-room
        }
        # close snowfield gate
        do remove flag snowfield-gate-open
        do modify room ice-pit {
            remove exit stargate-room
        }
        # open patio gate
        do add flag patio-gate-open
        do modify room patio {
            add exit "through the stargate" -> stargate-room
        }
    }
    if missing flag read-plaque-3 {
        do show "The fork-shaped button is already pressed down as far as it can go."
    }
}
