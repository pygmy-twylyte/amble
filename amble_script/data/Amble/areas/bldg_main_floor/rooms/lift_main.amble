# ========================================
# Elevator (Main Floor)
# ========================================
room lift-main {
    name "Elevator (Main Floor)"
    desc "The elevator interior is surprisingly luxurious -- polished chrome panels, soft lighting, and a faint scent of something that might be vanilla or data corruption."
    overlay if flag got-elevator-keycard {
        set "The [main] and [aa-3] buttons are now both lit -- but [aa-3] flickers and then glows intensely bright intermittently."
        unset "There are only two buttons: [main] is lit, but [aa-3] is dark."
    }
    exit "out to main floor" -> lift-bank-main
}

# ========================================
# Items
# ========================================

item aa_3_button_main {
    name "[AA-3] Button (Unlit)"
    desc "The button to send the elevator to sublevel AA-3, currently unlit and disabled."
    movability fixed "It's part of the elevator control panel."
    location room lift-main
}

item main_button_main {
    name "[Main] Button"
    desc "The button that sends the elevator to the main floor."
    movability fixed "It's part of the elevator control panel."
    location room lift-main
}

# ========================================
# Triggers
# ========================================

trigger "[Lift Main] Pressed AA-3 Button"
note "elevator aa_3 button controller"
when touch item aa_3_button_main {
    if all( has item elevator_keycard, has item visitor_pass ) {
        do push player to lift-sublevel-1
        do show "The elevator giggles and lurches gleefully downward, nearly taking you off your feet. Just as your spleen is becoming accustomed to sharing space with your tonsils, a sudden deceleration throws it all the way down into your pelvis and you come to an abrupt stop at the deep sublevel entrance."
    }
    if all( missing item elevator_keycard, missing item visitor_pass ) {
        do show "The elevator seems truly upset when it tells you it can't move unless you have a visitor pass *and* a proximity keycard."
    }
    if all( has item visitor_pass, missing item elevator_keycard ) {
        do show "With a sigh, the elevator acknowledges your psychic Visitor Pass (which currently identifies you as a master elevator technician) but reminds you that it can't budge unless you have a proximity card."
    }
    if all( missing item visitor_pass, has item elevator_keycard ) {
        do show "The elevator begins to move... and then freezes when it realizes that you have lost your visitor pass."
    }
}

trigger "[Lift Main]" when touch item main_button_main {
    do show "With something between a yawn and a sigh, the elevator reminds you that you are already on the main floor, then closes and opens the doors for emphasis."
}
