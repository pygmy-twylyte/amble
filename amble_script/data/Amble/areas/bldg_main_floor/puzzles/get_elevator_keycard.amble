# ========================================
# Elevator Keycard Puzzle
# ========================================

# prereq: have visitor pass (for VIP bathroom access)
# must take vogon poetry and insert in the poetry performer to open hidden panic room w/keycard

# ========================================
# Items
# ========================================

item vogon_poetry_book {
    name "Longest Poems of the Largest Vogons"
    desc "A bound volume of Vogon poetry, with the usual safety devices and locks removed. Looking inside is likely to cause brain damage and a permanent smell of boiling cauliflower to haunt you, if you're lucky."
    movability free
    location room vip-bathroom
    text """"Oh freddled gruntbuggly,
Thy micturations are to me
As plurdled gabbleblotchits on a lurgid bee.
Groop, I implore thee, my foonting turlingdromes,
And hooptiously drangle me with crinkly bindlewurdles,
Or I will rend thee in the gobberwarts
With my blurglecruncheon, see if I don't!"

"Bleem miserable venchit! Bleem
forever mestinglish asunder frapt.
Gashee morphousite,
thou expungiest quoopisk!
Fripping lyshus wimbgunts,
awhilst moongrovenly kormzibs.
Gerond withoutitude form into formless bloit,
why not then? Moose."
"""
    ability Read
}

item poetry_performer {
    name "Initech Poetry Performer"
    desc "In lieu of decades-old magazines, the office has installed this poetry recitation device. If poetic material is supplied, it is automatically performed in the whichever voice the listener finds most soul-penetrating (as long as it happens to be Morgan Freeman's)."
    movability fixed "It weighs about 500 kilograms. Are you hoping for a hernia?"
    location room b-a-office
    container state closed
}

# ========================================
# Triggers
# ========================================

let set keycard_hint_zone = ( b-a-office, vip-bathroom )
trigger "[HINT Radio] Find/Insert Vogon Poetry" only once
when give item invitation to npc b_a_receptionist {
    do schedule in 3 if any(
        all(
            has item hint_radio,
            has flag hint-radio-on,
            missing flag panic-room-open,
            in rooms keycard_hint_zone,
        ),
        has flag panic-room-open,
    ) onFalse retryNextTurn note "poetry performer radio hint" {
        if all(
            has item hint_radio,
            has flag hint-radio-on,
            missing flag panic-room-open,
            in rooms keycard_hint_zone,
        )
        {
            do npc says hint_radio_npc "The rumor around here is that the last candidate was vaporized while sealed in the hidden Poetry Panic Room at the office, and they had the sole remaining elevator keycard with them. The Poetry Performer will panic if you try to make it perform some Vogon poetry. I'm sure you see where this is going."
        }
    }
}

trigger "[Poetry-Panic] Acquired Elevator Keycard" only once
when take item elevator_keycard {
    do set npc state b_a_receptionist bored
    do add flag got-elevator-keycard
}

trigger "[Office] Insert Poems to Open Panic Room" only once
when insert item vogon_poetry_book into item poetry_performer {
    do award points 5 reason "Unlocked the poetry panic room with Vogon verse"
    do despawn item vogon_poetry_book
    do add flag panic-room-open
    do reveal exit from b-a-office to poetry-panic direction "poetry panic room"
    do add wedge "PA announcement: Vogon poetry has been detected in the building. If you are experiencing nausea, go to med bay for assistance." width 1 spinner ambientInterior
    do show "The machine begins to whine at a high pitch and gives off a noxious odor of burning hair. A klaxon blares and the display warns:\n\n            ⚠️ VOGON INPUT DETECTED - POETRY PANIC ROOM UNLOCKED ⚠️\n               ☣ VAPORIZING VOGON INPUT - AVOID TOXIC FUMES ☣\n\nThe whiteboard on the west wall slides to the side, revealing a vault-like door."
}
