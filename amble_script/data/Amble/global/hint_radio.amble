# ========================================
# HINT Radio
# ========================================

# The HINT Radio will be discovered at the guard post.
# When turned on and in inventory, it will deliver context-
# sensitive hints. When turned off, or not in inventory,
# it will be silent.

item hint_radio {
    name "HINT Radio (off)"
    desc r#"A black, long-range walkie-talkie style radio transceiver. A label bearing the word "HINTS" is affixed to the side. The transmit button has been removed. The "Power" LED is off."#
    portable true
    location room guard-post
    ability TurnOn
}

trigger "Turn Hint Radio On" when use item hint_radio ability turnOn {
    do modify item hint_radio {
        name "HINT Radio (on)"
        desc r#"A black, long-range walkie-talkie style radio transceiver. A label bearing the word "HINTS" is affixed to the side. The transmit button has been removed. The "Power" LED is on and soft static whispers from the speaker."#
        remove ability TurnOn
        add ability TurnOff
    }
    do add flag hint-radio-on
    do show "The radio clicks on and emits a soft static."
}

trigger "Turn Hint Radio On: First Time" only once
note "explanatory message and points only on first activation"
when use item hint_radio ability turnOn {
    do show "A soothing yet somehow disquieting and familiar voice flows from the radio's speaker."
    if has flag burned-fallen-tree {
        do npc says hint_radio_npc "Congratulations, Candidate, you've passed your first obstacle - though you lose a point for nearly starting a forest fire. I am your -- handler, let's say -- and this is your HINT radio. In some circumstances, I may be able to provide some extra direction to help you along -- but it looks better if you don't rely on me too heavily."
        do award points -1
    }
    if has flag chopped-fallen-tree {
        do npc says hint_radio_npc "Congratulations, Candidate. You've cleared the fallen tree, and you get a bonus point for choosing the hard path of chopping through the tree rather than starting a forest fire. I am your -- handler, let's say -- and this is your HINT radio. In some circumstances, I may be able to provide some extra direction to help you along -- but it looks better if you don't rely on me too heavily."
        do award points 1
    }
}

trigger "Turn Hint Radio Off" when use item hint_radio ability turnOff {
    do modify item hint_radio {
        name "HINT Radio (off)"
        desc r#"A black, long-range walkie-talkie style radio transceiver. A label bearing the word "HINTS" is affixed to the side. The transmit button has been removed. The "Power" LED is off."#
        remove ability TurnOff
        add ability TurnOn
    }
    do remove flag hint-radio-on
    do show "The radio clicks off."
}

# Trick here! Make HINT Radio an NPC that never spawns... but that doesn't prevent us from using the
# "npc says" trigger action, so it's like the radio "character" is talking to the player from nowhere.
npc hint_radio_npc {
    name "HINT Radio"
    desc "<Player should never see this since the NPC will never be spawned.>"
    location nowhere "never spawn -- used for voice from HINT radio"
    state normal
    dialogue normal {
        "(static)"
    }
}
