# ========================================
# Globally Useful Items
# ========================================

# This is a central location for any items that are "tools" that can be used in multiple locations
# throughout the game.

# ========================================
# Initech Arson-Aid
# ========================================

item arson_aid {
    name "Initech Arson-Aid"
    desc "A small personal flamethrower, for use in home and garden -- just not your own, if you know what's good for you. Instructions for use are labeled on the side."
    movability free
    location room two-sheds
    consumable {
        uses_left 1
        consume_on ability Ignite
        when_consumed replace current room melted_arson_aid
    }

    text """Instructions:
1) Aim carefully.
2) Slowly squeeze trigger.
3) Throw at target (quickly!)
4) Flee.
"""
    ability Ignite
    ability Read
}

trigger "[Global] Arson-Aid Used" only once
when use item arson_aid ability ignite {
    do show "The Initech Arson-Aid melts down into an amorphous, hissing blob. As expected."
}

item melted_arson_aid {
    name "Metallic Blob"
    desc "An irregular blob of metal and melted plastic that used to be an Initech Arson-Aid. Useless now. You can still just read the last step of the instructions on the side."
    movability free
    location nowhere "spawns wherever the arson-aid is used"
    text "4) Flee."
    ability Read
}

# ========================================
# Lumberjack Axe
# ========================================

item lumberjack_axe {
    name "Lumberjack's Axe"
    desc "A hefty lumberjack's axe. The blade is slightly rusted, but it looks usable."
    movability free
    location room two-sheds
    ability CutWood
    ability Smash
}

# ========================================
# Towel
# ========================================

item towel {
    name "Towel"
    desc "A plain, white nuclear bath towel with plooksilk stitching, fully charged."
    movability free
    location nowhere "spawns@player when delivery_box is opened"
    ability Insulate
    ability Clean
}

trigger "[Hint] Towel / Plaque-1 Hint" only once
when take item towel {
    do add flag got-towel
    do award points 3 reason "Recovered the essential towel every frood should carry"
    do show "NOW you're a frood who really \"knows where his towel is\"!"
    do schedule in 10 if all(
        has item towel,
        missing flag read-plaque-1,
        has flag tried-read-plaque-1,
    )
    note "hint: towels can clean things" {
        do show "Towels can be awfully handy for cleaning grimy things. Just sayin'."
    }
}

# ========================================
# Butane Lighter
# ========================================

item lighter {
    name "Butane Lighter"
    desc "An engraved, metallic Zippo-style lighter, worn but functional."
    movability free
    location room patio
    text "DJVB"
    ability Ignite
    ability Read
    consumable {
        uses_left 5
        consume_on ability Ignite
        when_consumed replace inventory empty_lighter
    }
}

item empty_lighter {
    name "Butane Lighter (Empty)"
    desc "An engraved, metallic Zippo-style lighter. It was good for a few uses but seems to have run out of fuel."
    movability free
    location nowhere "spawns@inventory when lighter used up"
    text "DJVB"
    ability Read
}

# ========================================
# Sonic Screwdriver
# ========================================

item sonic_screwdriver {
    name "Sonic Screwdriver"
    desc "A multipurpose marvel of alien engineering, capable of repairing complex machinery, unlocking sealed doors, and occasionally shorting out bureaucracies. Ineffective against wood, emotions, and existential dread."
    movability free
    location chest evidence_locker_open
    ability Repair
}

# ========================================
# Fishing Line
# ========================================

# needed to remove HAL module 2 (and/or tweezers) from crevice
item fishing_line {
    name "Makeshift Fishing Line"
    desc "A MacGyver-ed fishing line, with a paper clip hook attached to some dental floss."
    movability free
    location nowhere "spawns@inventory when items attached"
    ability Pluck
}

trigger "[Global] Fishing Line (attach clip to floss)"
when use item paper_clip on item dental_floss interaction attach
{
    do despawn item paper_clip
    do despawn item dental_floss
    do spawn item fishing_line in inventory
    do show "You MacGyvered the floss and paper clip into a fishing line!"
    do award points 3 reason "Improvised a fishing line from junk drawer staples"
}
trigger "[Global] Fishing Line (attach floss to clip)"
when use item dental_floss on item paper_clip interaction attach
{
    do despawn item paper_clip
    do despawn item dental_floss
    do spawn item fishing_line in inventory
    do show "You MacGyvered the floss and paper clip into a fishing line!"
    do award points 3 reason "Improvised a fishing line from junk drawer staples"
}
