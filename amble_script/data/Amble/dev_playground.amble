# ========================================
# Development Playground
# ========================================

# This file serves as a place to try new features or ideas without disturbing the main game source.

room dev-room {
    name "TESTING GROUND (DEV ONLY)"
    desc "This is however you imagine it, but a space for devs to try out new ideas before mixing in to other content."
    exit "back to game start" -> high-ridge
}

# this should allow us to create an exit from the start room (high-ridge) to the
# playground at game start (after 1 turn taken) so we don't have to put one in the actual
# start room file (so this file can simply be removed to cleanly get rid of playground
# items)
#
# you can also just comment out the trigger below to make the playground inaccessible from
# the game without deleting the file:
#
trigger "Create Dev Playground Exit" only once when always {
    do modify room high-ridge {
        add exit "to dev playground" -> dev-room
    }
}

item big_red_button {
    name "Big Red Button"
    desc "It's big, it's red, and it's a button! Press or touch it!"
    portable false
    location room dev-room
}

item big_blue_button {
    name "Big Blue Button"
    desc "It's big, it's blue, and it's a button! Press away!"
    portable false
    location room dev-room
}

item cheat_button {
    name "Cheat Button!"
    desc "Press this button to move this room to the final objective!"
    portable false
    location room dev-room
}

trigger "Cheat Mode" when touch item cheat_button {
    do show "Morphed the room into an endgame backdoor!"
    do modify room dev-room {
        name "AA-3B Back Entrance"
        desc "Where there used to be a High Isolated Ridge around you, there is now a blank white room with a door marked 'WIN!'"
        remove exit high-ridge
        add exit "win the game" -> room-aa-3b {
            required_items( no_cake ),
            barred "You must hold no cake to fathom Room AA-3B's mysteries."
        }
    }
}

npc the_luggage {
    name "The Luggage"
    desc "A wooden chest with dozens of tiny legs, layered sharp teeth around its lid, and a violent temper."
    location nowhere "spawns at dev-room for testing"
    max_hp 10
    dialogue normal {
        "(stares at you with menace)"
    }
}

trigger "Big Red Button Pushed"
note "test for the touch item command/event -- works!"
when touch item big_red_button {
    do show "Always never press the big red button! Now look what you've done!"
    do spawn npc the_luggage into room dev-room
}


trigger "Big Blue Button Pushed"
note "test modify npc"
when touch item big_blue_button {
    do show "Modifying the Luggage..."
    do modify npc the_luggage {
        name "Hungry Luggage"
        desc "The Luggage is ravenous! Beware!!"
        state custom(hungry)
        add line "(The Luggage nearly bites your face off when you approach.)" to state custom(hungry)
        random rooms( dev-room, lounge )
        timing every 5 turns
        active true
        loop true
    }
}

item poison_bottle {
    name "Furniture Poison"
    desc "Guaranteed to kill violent bits of luggage, dead."
    portable true
    location room dev-room
    ability Drink
    consumable {
        uses_left 2
        consume_on ability Drink
        when_consumed despawn
    }
}

item antidote {
    name "Health Potion"
    desc "Take in case of poison."
    portable true
    location room dev-room
    ability Drink
    consumable {
        uses_left 2
        consume_on ability Drink
        when_consumed despawn
    }
}

trigger "[DEV] Poison the Luggage"
when give item poison_bottle to npc the_luggage {
    if npc in state the_luggage normal {
        do npc refuse item the_luggage "Despite having no eyes, the Luggage glares at you, keeping its lid tightly shut."
    }
    if npc in state the_luggage custom:hungry {
        do show "The Luggage snaps at your hand and hungrily swallows the whole bottle."
        do damage npc the_luggage 5 for 4 turns cause "poison"
        do despawn item poison_bottle
    }
}

trigger "[DEV] Luggage Dies" when npc the_luggage dies {
    do show "The Luggage dies with a gasp, its tongue lolling out of the lid."
    do modify npc the_luggage {
        desc "The magnificent magical Luggage lies dead, lid ajar, tongue lolling and cut by the razor-sharp teeth lining the lid."
    }
}
