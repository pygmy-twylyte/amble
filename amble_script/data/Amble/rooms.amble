# Room definitions for Amble engine

room snowfield {
    name "Desolate Snowfield"
    desc "You find yourself in a wide, desolate snowfield west of the main building -- which is odd, because you still see summery woodlands just yards away, with a clean line separating the two climates. Wind howls across the ice, and powdery drifts swirl around a dark rectangular pit, some 20 feet across, but too deep and dark to see the bottom. A thick rope anchored deeply into the ice leads down into the pit."
    exit northeast -> loading-dock
    exit southeast -> front-entrance
    exit west -> snow-camp
    exit rope-down -> ice-pit
}

room ice-pit {
    name "Abysmal Ice Pit"
    desc "The bottom of a deep, sheer ice shaft seemingly dug for some kind of arctic research -- though you were nowhere near the Arctic Circle, last you checked. You're just able to see in the dim light filtering down from the tiny window to the surface above."
    exit rope-up -> snowfield
}

room snow-camp {
    name "Abandoned Campsite"
    desc "The remains of a temporary shelter are dug into a snowbank here. The tent has collapsed, half-fused to a scorched sled. The river â€” or some frozen remnant of it â€” glistens beyond."
    exit east -> snowfield
}

room main-lobby {
    name "Main Lobby"
    desc "The lobby is clinically clean in a way that feels suspicious. A slightly humming vending machine stands beside an empty podium bearing an emblem that reads, in gothic script: 'ALLIED TEMPORARY LIFEFORMS'. A softly glowing sign on the north wall reads 'Elevator Access', while arrows on the east and west walls point toward other destinations with less enthusiasm."
    overlay if npc gonk_droid here {
        normal "A gonk droid stands idle in the corner, muttering to itself and tottering in circles."
        happy  "The gonk droid gonks a happy tune in the corner, charging the battery you gave it."
    }
    overlay if npc cmot_dibbler here {
        normal "C.M.O.T. Dibbler hawks dubious sausages to anyone entering."
        happy  "Dibbler grins broadly, offering a complimentary bite."
        bored  "Dibbler mutters about slow lobby sales."
    }
    exit north -> lift-bank-main
    exit south -> front-entrance
    exit east -> restaurant
    exit west -> b-a-office
}

room b-a-office {
    name "Amble Adventures Main Office"
    desc "The office of Amble Adventures, a wholly disowned subsidiary of Brains Anonymousâ„¢, is a shrine to barely-funded creativity. Faded posters of games that were never released adorn the walls, including one titled 'PONG Construction Set 3000'. A sagging ficus looms in the corner like itâ€™s been through something. An Initechâ„¢ Poetry Performer plays possum in another corner, hoping you don't notice it."
    overlay if flag panic-room-open {
        set r#"The "Planning to Plan" whiteboard has parted to reveal a heavy, vault-like door set into the west wall."#
        unset r#"A floor-to-ceiling whiteboard on the west wall boasts an impressive flowchart titled "Planning to Plan"."#
    }
    overlay if (flag unset got-visitor-pass, player missing item invitation) {
        text "A robotic receptionist sits behind a tidy desk adorned with figurines of Portal turrets and companion cubes. She (it?) pointedly ignores you."
    }
    overlay if (flag unset got-visitor-pass, player has item invitation) {
        text "The receptionist, sensing something, tries furtively to see what you have in your pack."
    }
    overlay if (flag set got-visitor-pass) {
        text "The receptionist looks up from rearranging her turrets, giving you a subtle wink and a grin."
    }
    overlay if (flag set lost-and-found-opened) {
        text "The Lost & Found box sits with its lid hanging open, contents plainly visible."
    }
    exit east -> main-lobby
    exit north -> supply-closet
}

room lift-bank-main {
    name "Elevator Bank (Main Floor)"
    desc "A bank of elevators waits here, none of which seem eager to go anywhere. One of them is open and labeled 'MAIN LIFT' in glowing teal script.  To the east, a door marked 'LOUNGE' is just slightly ajar, as if luring you to come in."
    overlay if flag got-elevator-keycard {
        set "The proximity card reader next to the lift now somewhat sheepishly displays: ACCESS GRANTED."
        unset "The proximity card reader next to the lift displays: ACCESS DENIED -- and with no sympathy whatsoever."
    }
    overlay if npc gonk_droid here {
        normal "A gonk droid pokes at the elevator call panel, emitting perplexed gonks."
        happy  "The gonk droid hums a cheerful gonk as it watches the lift lights blink."
    }
    overlay if npc cmot_dibbler here {
        normal "Dibbler loiters by the elevators, promising snacks for the ride."
        happy  "Dibbler beams, certain elevator patrons are his best customers."
        bored  "Dibbler counts his sausages as the lift lights blink listlessly."
    }
    exit north -> lift-main
    exit east -> lounge
    exit south -> main-lobby
}

room lift-main {
    name "Elevator (Main Floor)"
    desc "The elevator interior is surprisingly luxurious -- polished chrome panels, soft lighting, and a faint scent of something that might be vanilla or data corruption."
    overlay if flag got-elevator-keycard {
        set "The [main] and [aa-3] buttons are now both lit -- but [aa-3] flickers and then glows intensely bright intermittently."
        unset "There are only two buttons: [main] is lit, but [aa-3] is dark."
    }
    exit main -> lift-bank-main
    exit aa-3 -> lift-sublevel-1 {
        required_items(visitor_pass, elevator_keycard),
        barred "That button is disabled. Visitor pass and elevator keycard are required for access to the sublevel."
    }
}

room lounge {
    name "Coffee Lounge"
    desc "A small space to relax, with a worn sofa, a ringed coffee table bearing a few magazines, and part of a jigsaw puzzle depicting the USS Enterprise D."
    overlay if item lebowski_rug {
        present "An ornate, brick red rug lies in the center of the room and somehow -- really ties the room together."
        absent "The trap door opened in the floor reveals a ladder down to a hidden room below."
    }
    overlay if (flag set lab-fire-raging) {
        text "Occasional wisps of black smoke rise from the portal room below."
    }
    overlay if flag coffee-brewed {
        set "The coffee machine emits a few satisfied clicks as the rich aroma of fresh coffee fills the lounge."
        unset "An automatic drip coffee machine stands empty but ready to brew on a small stand in one corner."
    }
    overlay if npc gonk_droid here {
        normal "The gonk droid mills about by the coffee cart, muttering quiet gonks."
        happy  "The gonk droid gonks to an unheard upbeat rhythm by the sofa."
    }
    overlay if npc cmot_dibbler here {
        normal "Dibbler lounges here, pitching snacks to passing staff and adventurers."
        happy  "Dibbler cheerfully offers samples to anyone near the sofa."
        bored  "Dibbler stares at the coffee pot, wondering if he can sell the used grounds somehow."
    }
    exit west -> lift-bank-main
}

room portal-room {
    name "Portal Room"
    desc "A bare white room with a white tile floor. A ladder leads back up through the trap door in the ceiling. Three of the four walls are used as whiteboards, littered with obscure diagrams and equations. In the center, a black and white Portal gun is affixed to a pedestal and aimed squarely at the fourth wall."
    overlay if (flag set portal-gun-powered) {
        text "The power indicator on the Portal gun now glows a steady green."
    }
    overlay if flag portal-opened {
        set "There is a sparkling blue portal in the fourth wall, with a view through to a laboratory space beyond."
        unset "A bullseye adorns the fourth, otherwise blank wall. A photo of a watch-sized computer attached to a potato battery is taped in the center, for some reason."
    }
    overlay if (flag set portal-opened, flag set lab-fire-raging) {
        text "Black smoke and the occasional lick of flame pour through the portal from the laboratory inferno on the other side."
    }
}

room aperture-lab {
    name "Aperture Science Laboratory"
    desc "The unnaturally whiteâ€”walls, floor, ceiling, even the air here feels clinical and overexposed. Everything hums at a frequency designed to encourage strict compliance. A single console blinks on a table beside a highly over-engineered printer labeled \"Aperture Science Hospitality Generator.\""
    overlay if (flag set burned-invitation, flag unset invitation-sans-ignition) {
        text "A smoky haze and the lingering smell of burned paper permeate the room."
    }
    overlay if (flag set lab-fire-raging) {
        text "Flames leap from foamy surfaces around you, while the sprinklers try in vain to douse the oily residue!"
    }
    overlay if (flag complete foam-fire-in-lab, flag unset invitation-sans-ignition) {
        text "Charred, soggy and warped lab and office equipment are everywhere. Astoundingly, the printer still seems to be working -- and with a job pending."
    }
    overlay if (flag set invitation-sans-ignition) {
        text "The printer is in its 3-day cooldown period, and there are no jobs pending."
    }
    overlay if (item present invitation) {
        text "A glowing, steaming, sizzling invitation engraved on an asbestos sheet sits on the floor in front of the printer."
    }
    exit portal -> portal-room
}

room restaurant {
    name "Restaurant at the End of the Universe"
    desc "Soft jazz wafts through invisible speakers, accompanied by the occasional sound of imploding galaxies. A tasteful sign declares this to be the Restaurant at the End of the Universe, though it appears to be between dinner rushes. Tables float slightly above the ground and rearrange themselves when you're not looking. A host's podium displays a reservation list. Restrooms lie to the south bearing a sign in fancy script reading:\n\n                             VIP PAN-SPECIES RESTROOM\n                       (must possess visitor pass to enter)\n\nPatio seating stretches farther east."
    overlay if npc cmot_dibbler here {
        normal "Dibbler circles the tables, insisting his sausages are a bargain."
        happy  "Dibbler proclaims today's special with exaggerated flair."
        bored  "Dibbler pokes at an empty table, grumbling about the quiet rush."
    }
    exit west -> main-lobby
    exit east -> patio
    exit south -> vip-bathroom {
        required_items(visitor_pass),
        barred "The door apologizes profusely for refusing to open, explaining that it's forced by programming not to let you in without a visitor's pass."
    }
}

room vip-bathroom {
    name "VIP Bathroom"
    desc "Blinding white tile and pristine mirrors make it almost seem like this pan-gender, pan-species bathroom has never been used, but a lingering asparagus odor suggests otherwise."
    exit north -> restaurant
}

room patio {
    name "Restaurant Patio"
    desc "The patio overlooks nothing in particularâ€”just an endless starfield gently swirling beyond a transparent force field. A few metal tables are bolted to the floor, and one of them has a half-empty cocktail glass slowly un-drinking itself. A sign near the edge reads: 'No Leaning Beyond This Point. You have been warned.'"
    exit west -> restaurant
}

room poetry-panic {
    name "Bad Poetry Panic Room"
    desc "A concrete and titanium vacuum-sealed room equipped with noise generators, active voice-cancelling harmonics and Cone of Silence technology. A seal by the door proudly boasts that the chamber is almost completely Vogon poetry-proof."
    exit east -> b-a-office
}

room lift-sublevel-1 {
    name "Elevator (Sublevel AA-3)"
    desc "The elevator sits open at the end of the corridor, but its appearance isâ€¦ altered. What was once a sleek, polished carriage now seems borrowed from another world entirely. Gilded scrollwork curls along the walls where none existed before, and the ceiling flickers between brass panels and a starlit void. It hums with potentialâ€”and something less easily named."
    exit main -> lift-main
    exit south -> sublevel-1-entrance
}

room sublevel-1-entrance {
    name "Sublevel AA-3 Entrance"
    desc "A small landing area covered in dust. Buckets, tools, and debris are strewn about the floor. The door to the west is labeled with a sign:\n\n                        ðŸš§ ROOM AA-3B ðŸš§\n                 FICTIONAL UNIVERSE CONTAINMENT"
    overlay if (npc in state black_knight normal) {
        text "The Black Knight stands in your path, staring you down, leaning on his battered longsword."
    }
    overlay if (npc in state black_knight mad) {
        text "Agitated, the Black Knight sidesteps around the room, occasionally lunging at you to grab at his sword or kick you in the shin."
    }
    overlay if (npc in state black_knight happy) {
        text "The Black Knight stands in a corner, helmet off, gleefully plucking hairs from his beard and splitting them with his sword."
    }
    exit north -> lift-sublevel-1
    exit west -> room-aa-3b {
        required_flags(appeased-black-knight, touched-monolith),
        barred "The Black Knight stands in your way."
    }
    exit east -> security-station {
        required_flags(appeased-black-knight),
        barred "The Black Knight stands in your way."
    }
    exit south -> corridor-north {
        required_flags(appeased-black-knight),
        barred "The Black Knight stands in your way."
    }
}

room supply-closet {
    name "Supply Closet"
    desc "A small closet lit by a bare bulb being repeatedly rammed by a fluttering moth. Crowded shelves offer paper products, bottles of toner, staples, and other office... ... staples."
    exit south -> b-a-office
}

room room-aa-3b {
    name "Room AA-3B"
    desc "The door opens into a swirling mass of static and fractured reality. Flickering projections of impossible geometries ripple through the darkness. Objects seem to vanish halfway through existing. A guttural voice whispers paradoxes from the walls themselves. Anyone with a sense of self-preservation should probably leave immediately.\n\nA small sign by the entrance reads: 'Amble Adventures assumes no responsibility for ontological damage incurred within.'"
    exit east -> sublevel-1-entrance
}

room corridor-north {
    name "Research Corridor (North)"
    desc "A dimly lit concrete hallway stretches out, lined with emergency lights that flicker like they're deciding whether to stay on. Faint scratching sounds echo through the walls at irregular intervals.\n\nGraffiti on one wall reads: 'THEY NEVER LEFT AA-3B.' A maintenance hatch rattles occasionally, as if something on the other side is trying to remember how to use a doorknob."
    exit north -> sublevel-1-entrance
    exit west -> observation-room
    exit east -> med-bay
    exit south -> corridor-south
}

room observation-room {
    name "Field Observation Room"
    desc "Thick observation windows, long since spiderwebbed with cracks, overlook what should be Room AA-3Bâ€”but only darkness and occasional flashes of unreality appear beyond. Abandoned swivel chairs and battered clipboards litter the room. A faint smell of burnt ozone hangs in the air.\n\nA console near the glass flickers with a repeating message: 'CONTAINMENT BREACH LOGGED â€“ STATUS: UNRESOLVED.'"
    exit east -> corridor-north
    exit south -> monolith-room
}

room security-station {
    name "Level AA-3 Security"
    desc "A compact but well-equipped station with reinforced blast doors, a wall of cracked CRT monitors, and a swivel chair. The space above the monitors is emblazoned with 'IN CASE OF EMERGENCY, RUN FASTER.' Paperwork is scattered across the floor, including an ominous memo titled 'AA-3B: Risk Management Recommendations.'"
    overlay if item evidence_locker_closed {
        present "A heavy steel evidence locker is embedded in the wall. It's covered in dents, scratches, and scorch marks -- evidence of previous failed attempts to open it."
        absent "The heavy door of the evidence locker, having surrendered at last, hangs loosely from one of its hinges."
    }
    exit west -> sublevel-1-entrance
}

room med-bay {
    name "Med Bay"
    desc "A clean white infirmary with two bio-beds extending from the back wall, surrounded by a bevy of monitors and robotic surgical tools. One of the machines goes \"Bing!\" every few minutes. A small cleaning robot floats around the room disinfecting things."
    overlay if npc emh here {
        normal "The EMH stands with professional detachment, awaiting your symptoms."
        happy  "The EMH smiles and hums a bright tune while adjusting the diagnostic displays."
        custom(want-emitter) "The EMH fidgets restlessly, casting longing glances toward the door as if craving a mobile emitter."
    }
    exit west -> corridor-north
}

room corridor-south {
    name "Research Corridor (South)"
    desc "The research corridor continues here, though the walls are noticeably more patched and hastily reinforced. Warning stripes mark a wide door to the west, and faint heat shimmers pulse across the floor, as if reality itself is running a fever. Somewhere far too close, a light flickers in time with your heartbeat."
    exit north -> corridor-north
    exit west -> monolith-room
}

room monolith-room {
    name "Monolith Room"
    desc "The room is stark, dim, and cavernous. A series of industrial floodlights cast long, angled shadows across the centerpiece: a tall, perfectly black monolith that defies reflection. It absorbs light, sound, andâ€”if you stare too longâ€”possibly thought."
    overlay if (flag unset talked-to-hal) {
        text "A HAL-9000 camera and terminal embedded in the far wall looms dark and silent, but somehow still seems to be observing your actions."
    }
    overlay if (flag set hal-reboot#1) {
        text "Several lights blink slowly on a panel near HAL-9000's camera, whose red light still glows faintly. A few cryptic instructions flicker across the monitor."
    }
    overlay if (flag set hal-reboot#2) {
        text "HAL-9000's lens light glows bright red with a yellow center. Lights on several panels now flash in sequence; a boot sequence scrolls rapidly up the display."
    }
    overlay if (flag complete hal-reboot) {
        text "The light in HAL-9000's lens glows an intense red and seems to stare through you and into the monolith. Various status notifications cycle on the nearby monitor."
    }
    exit east -> corridor-south
    exit north -> observation-room
}
