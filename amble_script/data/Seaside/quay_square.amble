# ================================================
# Quay Square (Start Location)
# ================================================

room quay-square {
  name "Quay Square"
  desc "The town square, adjoining the harbor quay. Gulls call overhead, looking for supper below. The air smells of brine and tar and frying oil. A somewhat brackish-appearing fountain with a centerpiece of frolicking dolphins decorates the center of the square. The bright turquoise awning of the fish market beckons in one direction, and a path leads around to a slipway. Over by the tar barrels, weathered stone steps wind up toward the lighthouse."
  visited false
  overlay if (item present tiny_package) {
    text "The tiny package that the heron dropped off lies on a bench nearby -- a little battered, but intact."
  }
  exit "along the slipway" -> boathouse-slipway
  exit "beneath the awning" -> fish-market-shed
  exit "past the tar barrels" -> net-yard
  exit "up the stone steps" -> lighthouse-path
}

# =================================================
# Items
# =================================================

item tiny_package {
  name "Tiny Package"
  desc "A tiny gift box sitting on a bench in the Quay Square, smaller than a pack of cards and significantly lighter. It's wrapped impossibly tightly in several crossed winds of strong twine."
  portable true
  location nowhere "scheduled spawn@quay-square after locket taken"
  container state closed
  requires cut to open
}

item tiny_portrait {
    name "Miniscule Portrait"
    desc "A very small, very worn and faded portrait of... someone, cut into an oval shape. There's a mildly sticky substance on the back."
    portable true
    location chest tiny_package
}

item sausage_inna_bun {
    name "Fishy Sausage-inna-Bun"
    desc "A hot dog bun, stuffed with a sausage of aquatic -- and fishy -- provenance."
    location npc cmot_dibbler
    restricted true
    portable true
    ability Eat
    consumable {
        uses_left 1
        consume_on ability Eat
        when_consumed despawn
    }

}

# =================================================
# NPCs
# =================================================

npc cmot_dibbler {
    name "Cut-Me-Own-Throat Dibbler"
    desc "A wily but unwise street vendor always looking to make a buck. He gets his name from his common cry that he would have to \"Cut Me Own Throat!\" to give you a better deal on his (often questionable) wares."
    location room quay-square
    movement random rooms( quay-square, fish-market-shed, net-yard ) timing every_3_turns active false
    state bored
    dialogue bored {
        "Sure is dead around here. Who am I going to sell my Fishy Sausage-Inna-Bun to?"
        "Not many customers left -- I should probably go in before the Lighthouse Crone comes out to wander."
        "Friend, you really should try my Fishy Sausage-Inna-Bun! Rated Quay Snack of the Year two seasons ago."
        "By the way, the Fishy Sausage-Inna-Bun is a great firestarter. It will burn for about 4 hours. Just don't breathe much by it."
        "Wanna try a Fishy Sausage-inna-Bun? Just one Sand Dollar!"
    }
    dialogue custom sold-fishy-sausage {
        "Ah, my best and only customer -- well, today anyway! Sorry, but I'm fresh out of Fishy Sausage -- I'll have some soon!"
    }
}

# =================================================
# Triggers
# =================================================
trigger "Cut Twine With Knife" only once
when use item fillet_knife on item tiny_package interaction open {
    do show "Using the knife, you work carefully to free the package from its wrapping without damaging it..."
}

trigger "Cut Twine / Use 'Open' Hint" note "'cut' won't open the package, so give hint to use 'open'"
when use item fillet_knife on item tiny_package interaction cut {
    if chance 50% {
        do show "You feel like you've cut through enough of the twine that you can open the package with the tip of the knife."
    }
}

trigger "Cut Open Tiny Package" only once note "requires ItemAbility::Cut"
when act open on item tiny_package {
    do show "You easily cut through the twine holding the package shut."
    do set container state tiny_package open
    do award points 5
}

trigger "Activate CMOT" when talk to npc cmot_dibbler {
    do npc says cmot_dibbler "Not so many customers around in the evenings because of the Crone. I'll guess I might as well go scrounge -- er -- resupply me fish in a bit."
    do set npc active cmot_dibbler true
}

trigger "Buy Fishy Sausage-inna-Bun"
when give item sand_dollar to npc cmot_dibbler {
    do despawn item sand_dollar
    do give item sausage_inna_bun to player from npc cmot_dibbler
    do set npc state cmot_dibbler custom:sold-fishy-sausage
    do npc says cmot_dibbler "Another satisfied customer -- enjoy! I'll restock soon, so tell a friend to come see C.M.O.T!"
    do award points 5
}

trigger "Eat Fishy Sausage-inna-Bun"
note "triggers indigestion x 10 turns"
when eat item sausage_inna_bun {
    do add flag status:indigestion
    do show "You're pretty sure there were bits of rope and pebbles in the sausage, but you eat it anyway. Your stomach begins to protest."
    do schedule in 10 note: "indigestion ends" {
        do remove flag status:indigestion
        do show "Your guts finally begin to feel somewhat normal again."
    }
}

trigger "Status: Indigestion" when always {
    if all(chance 50%, has flag status:indigestion) {
        do spinner message Indigestion
    }
}

# ========================================
# Spinners
# ========================================
spinner Indigestion {
    wedge "Your stomach cramps. You worry there's no public restroom around."
    wedge "Your gut drops as if you were on a roller coaster."
    wedge "The fish burps are -- worse than the Fishy Sausage-inna-Bun that caused them."
    wedge "Your stomach complains so loudly that a nearby squirrel is startled away."
}
