# ================================================
# Lighthouse Path
# ================================================
room lighthouse-path {
    name "Lighthouse Path"
    desc "Here weathered stone steps -- made slippery by mists and a healthy coating of algae -- slowly wind their way up a wind-battered cliff face toward the lighthouse. The spray in the air dampens your clothes, and a thick rope railing is all that stands between you and the crashing surf below."
    visited false
    exit "down the steps" -> quay-square
    overlay if item gold_locket {
        present "A golden locket, hitched to the railing with its thread-like chain, swings gently in the coastal winds."
        absent "Low, mournful moans can be heard between the periodic roar of the surf below. It seems to be coming from the lighthouse high above."
    }
}

# ================================================
# Items
# ================================================

item gold_locket {
    name "Golden Locket"
    desc "A locket of gold on a chain, looped over the rope railing on the lighthouse path; it sways to and fro in the offshore gusts of wind. It isn't very worn, and appears not to have been here very long."
    portable true
    location room lighthouse-path
    container state open
    text "When opened, the locket contains a photo of an older, graying man on one side. There is glue residue on the other side, but the portrait has been removed. An inscription engraved on the back reads: 'To P-- My Love'."
    ability Read
}

item restored_locket {
    name "Golden Locket"
    desc "A locket of gold on a chain, looped over the rope railing on the lighthouse path; it sways to and fro in the offshore gusts of wind. It isn't very worn, and appears not to be very old."
    portable true
    location nowhere "spawns when portrait replaced"
    text "When opened, the locket contains a photo of an older, graying man on one side and a smiling, radiant woman on the other. An inscription engraved on the back reads: 'To P-- My Love'."
    ability Read
}
# ================================================
# Triggers
# ================================================

trigger "Locket Taken" only once when take item gold_locket {
    do award points 10
    do add seq flag return-locket limit 4
    #0 -> locket taken
    #1 -> portait found
    #2 -> portrait put in locket
    #3 -> locket taken to lighthouse by heron
    #4 -> meet NPC wearing locket coming down lighthouse path
    do show "You loop the locket back through its chain to free it from the rope. It feels familiar in your hand, somehow."
    do set item description gold_locket "The gold locket you found looped over the rope railing on the lighthouse path. It isn't very worn, but somehow feels familiar in your hand."
    do schedule in 3 if has item gold_locket onFalse retryNextTurn note "first heron sighting" {
        do show "You notice a beautiful, heron-like bird circling close by."
        do schedule in 3 if has item gold_locket onFalse retryNextTurn note "second heron sighting" {
            do show "You notice the heron again, gliding easily on the wind gusts toward the quay square."
            do schedule in 3 if all(has item gold_locket, player in room quay-square) onFalse retryNextTurn note "heron box delivery" {
                do show "The beautiful seabird that's been following you lands on a nearby lamppost carrying something in its beak. After a moment, it drops it at your feet before soaring off over the quay and back into the harbor."
                do spawn item tiny_package into room quay-square
            }
        }
    }
}

trigger "Locket: Portrait Found" only once 
note "advances return-locket to #1"
when take item tiny_portrait {
    do advance flag return-locket
    do award points 5
}

trigger "Dropped Locket"
when drop item gold_locket {
    do show "The moment you drop the locket, you begin to miss it -- and feel like you've **been** missing it for a long time."
    do add flag status:longing
}

trigger "Status: Longing"
when always {
    if all(chance 30%, has flag status:longing) {
        do spinner message Longing
    }
}

trigger "Picked Up Locket Again"
when take item gold_locket {
    if flag in progress return-locket {
        do show "You feel better now that you have the locket back in your hand, like your best and oldest friend."
        do remove flag status:longing
    }
}

trigger "Replace Locket Portrait" only once
note "advances return-locket to #2"
when insert item tiny_portrait into item gold_locket {
    do add flag locket-restored
    do advance flag return-locket
    do replace item gold_locket with restored_locket
    do award points 5
}

# ================================================
# Goals
# ================================================
goal locket-mystery {
    name "Mysterious Locket"
    desc "Uncover the secret of the Golden Locket."
    group optional
    start when flag in progress return-locket
    done when flag complete return-locket
}

goal find-portrait {
  name "Lost and.. Found?"
  desc "You found a locket on the path up to the lighthouse, but one of the pictures is missing. Maybe you can find the owner if you can find the missing portrait?"
  group optional
  start when flag in progress return-locket
  done when has item tiny_portrait
}

goal place-portrait {
    name "Replace the Portrait"
    desc "Return the tiny portrait to its proper place."
    group optional
    start when goal complete find-portrait
    done when has flag locket-restored
}

# ========================================
# Spinners
# ========================================
spinner Longing {
    wedge "Your thoughts drift back to that golden locket again."
    wedge "Thinking back to the locket... did you recognize the man? Where did you leave it?"
    wedge "You find your hand checking your pocket for the locket... again."
}
